import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

apply plugin: 'java'
apply plugin: 'application'

group = 'com.halotroop'
archivesBaseName = 'BrightCraft'
version = '0.1.0'

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://jitpack.io/" } // Github-only libraries
}

jar {
    manifest {
        attributes(
                'Main-Class': 'com.halotroop.brightcraft.GlowTest'
        )
    }
    exclude("**/com/halotroop/brightcraft/debug/")
}

dependencies {
    compile       (files('README.md'))
    // Lightweight Java Game Library
    compile lwjgl ('glfw', 'opengl', 'stb')
    compile       ("org.joml:joml:1.9.12")

    // Google Libraries
    compile       ("com.google.guava:guava:28.2-jre")
    compile       ("com.google.code.findbugs:jsr305:3.0.2")

    // Game features supporting libraries
    implementation("blue.endless:jankson:1.2.0") // Configuration save format
    implementation("org.mongodb:bson:4.1.0") // World save format

    implementation("com.github.falkreon:LibNoiseJava:260e0bb") // Noise generator
    implementation("com.github.playsawdust:Glow:dfba54b")  // Render engine
}

def lwjgl(String... modules) {
    def ver = project.lwjgl_version
    def os = DefaultNativePlatform.currentOperatingSystem.toFamilyName()

    def libs = []

    def path = 'org.lwjgl:lwjgl'

    libs.add(dependencies.create("${path}:${ver}"))
    libs.add(dependencies.create("${path}:${ver}:natives-${os}"))
    modules.each { module ->
        libs.add(dependencies.create("${path}-${module}:${ver}"))
        libs.add(dependencies.create("${path}-${module}:${ver}:natives-${os}"))
    }
    return libs
}
